var
  // часы
  Digit, ySlide: array[0..8] of integer; 

  // вагоны
  VagonCount, TelsCount: integer;
  tels: array[0..5] of integer;
//  VagonName: array[0..3] of string;
  WheelBase: extended;
  MinTime, MaxTime: extended;

  t: single;
  StopTime: integer;
  Period: integer;


function Path2Segment(TrainNumber: integer; L: extended): integer;
var LCalc, HCalc, iCalc: integer;
begin
  //PathLength и PathLength2 - это единый массив, поэтому если TrainNumber=1 добавляем NodeCount к SFrom, STo
  LCalc := NodeCount * TrainNumber;
  HCalc := (NodeCount - 1) + NodeCount * TrainNumber;

  // ищем, в каком сегменте половинным делением
  if PathLength[HCalc] <= L then
    LCalc := HCalc;
  while LCalc < HCalc - 1 do begin
    iCalc := (LCalc + HCalc) shr 1;
    if PathLength[iCalc] <= L then
      LCalc := iCalc
    else
      HCalc := iCalc;
  end;
  Result := LCalc;

//    Result := Result - NodeCount * TrainNumber;
end; // Path2Segment

function Segment2Path(TrainNumber: integer; Segment: extended): extended;
var iSegment: integer;
    SegmentPart: extended;
begin
  iSegment := Trunc(Segment);
  if iSegment < 0 then
    iSegment := 0
  else if iSegment > NodeCount - 1 then
    iSegment := NodeCount - 1;
  SegmentPart := Segment - iSegment;

  if (iSegment <= (TrainNumber + 1) * NodeCount - 1) or (SegmentPart <= 0) then
    Result := PathLength[iSegment + TrainNumber * NodeCount] + SegmentPart * (PathLength[iSegment + 1 + TrainNumber * NodeCount] - PathLength[iSegment + TrainNumber * NodeCount])
  else
    Result := PathLength[iSegment + TrainNumber * NodeCount] + SegmentPart * (PathLength[iSegment + TrainNumber * NodeCount] - PathLength[iSegment - 1 + TrainNumber * NodeCount]);
end; // Segment2Path

procedure InitClock();
begin
  // смещение каждого разряда часов относительно предыдущего
  ySlide[0] := 0; 
  ySlide[1] := 5;
  ySlide[2] := 6;
  ySlide[3] := 5;
  ySlide[4] := 6;
  ySlide[5] := 5;
  ySlide[6] := 10;
  ySlide[7] := 6;
  ySlide[8] := 5;
end;

procedure InitTrain();
//var VagonLength, TailVagonLength, HeadWheelShift, VagonWheelBase: extended;
begin
//  VagonLength := 287.7;
//  TailVagonLength := 290;
//  VagonWheelBase := 157.5;
  TelsCount := 6;
  WheelBase := 44;
  MinTime := -8;
  MaxTime := 25;

//  // координаты тележек
  tels[0] := 0;
  tels[1] := -310.36;
  tels[2] := -615.5;
  tels[3] := -796.4;
  tels[4] := -1101.6;
  tels[5] := -1412;

//  VagonName[0] := 'Head';
//  VagonName[1] := 'Middle';
//  VagonName[2] := 'Middle';
//  VagonName[3] := 'Tail';
end; // InitTrain

procedure DrawClock(Departure: integer);
var i: integer;
    Hour, Mn, sec, ms: word;
begin
  // рисуем часы
//exit;
  DecodeTime(Time, Hour, Mn, Sec, MS);

  Digit[0] := Hour div 10;
  Digit[1] := Hour mod 10;
  Digit[2] := Mn div 10;
  Digit[3] := Mn mod 10;
  Digit[4] := Sec div 10;
  Digit[5]:= Sec mod 10;

// время отправления предыдущего поезда - три последние цифры
  if Departure >= 600 then begin
    Digit[6] := -1;
    Digit[7] := -1;
    Digit[8]:= -1;
  end else begin
    Mn := Departure div 60;
    Departure := Departure mod 60;
    Digit[6] := Mn;
    Digit[7] := Departure div 10;
    Digit[8]:= Departure mod 10;
  end;

  Draw3dObject('Clock.ClockBody');

  // цифры
  glPushMatrix;
  for i := 0 to 8 do
    if (Digit[i] >= 0) and (Digit[i] <= 9) then begin
      glTranslatef(0, -ySlide[i], 0);
      Draw3dObject('Clock.Digit' + IntToStr(Digit[i]));
    end;
  glPopMatrix;
end; // DrawClock

function GetPeriod(Hour: integer): integer;
var P: integer;
begin
  // период между поездами по часам в сутках
  if      Hour < 2 then  P := 10
  else if Hour < 5 then  P := 10000
  else if Hour < 7 then  P := 5
  else if Hour < 10 then P := 1
  else if Hour < 17 then P := 2
  else if Hour < 20 then P := 1
  else if Hour < 21 then P := 2
  else if Hour < 22 then P := 3
  else                   P := 5;

  Result := P * 60; // переводим минуты в секунды
end; // GetPeriod

procedure CalcWheel(TrainNumber: integer; 
                    L: extended;
                    var WheelX: extended;
                    var WheelY: extended;
                    var WheelZ: extended;
                    var AngleXY: extended;
                    var AngleZ: extended);
var Segment: integer;
    SegmentPart: extended;
begin
  // ищем, в каком сегменте
  Segment := Path2Segment(TrainNumber, L);
  // рассчитываем долю сегмента
  if (Segment + 1) mod NodeCount = 0 then begin
    SegmentPart := (L - PathLength[Segment]) / (PathLength[Segment] - PathLength[Segment - 1]);
    WheelX := Xs[Segment] + SegmentPart * (Xs[Segment] - Xs[Segment - 1]);
    WheelY := Ys[Segment] + SegmentPart * (Ys[Segment] - Ys[Segment - 1]);
    WheelZ := Zs[Segment] + SegmentPart * (Zs[Segment] - Zs[Segment - 1]);
    AngleXY := Arctan2(Ys[Segment - 1] - Ys[Segment], Xs[Segment - 1] - Xs[Segment]) * 57.3;
    AngleZ := -Arctan2(Zs[Segment] - Zs[Segment - 1], sqrt(sqr(Xs[Segment] - Xs[Segment - 1]) + sqr(Ys[Segment] - Ys[Segment - 1]))) * 57.3;
  end else begin
    SegmentPart := (L - PathLength[Segment]) / (PathLength[Segment + 1] - PathLength[Segment]);
    WheelX := Xs[Segment] + SegmentPart * (Xs[Segment + 1] - Xs[Segment]);
    WheelY := Ys[Segment] + SegmentPart * (Ys[Segment + 1] - Ys[Segment]);
    WheelZ := Zs[Segment] + SegmentPart * (Zs[Segment + 1] - Zs[Segment]);
    AngleXY := Arctan2(Ys[Segment] - Ys[Segment + 1], Xs[Segment] - Xs[Segment + 1]) * 57.3;
    AngleZ := -Arctan2(Zs[Segment + 1] - Zs[Segment], sqrt(sqr(Xs[Segment + 1] - Xs[Segment]) + sqr(Ys[Segment + 1] - Ys[Segment]))) * 57.3;
  end;
end; // CalcWheel

// процедура рисует путь шариками, в тестовых целях
procedure DrawPath(r: integer);
var i, j: integer;
    X, Y, Z, AXY, AZ: extended;
begin
  for j := 0 to TrainCount - 1 do begin
    for i := 0 to NodeCount - 1 do
      DrawSphere(xs[i + j * NodeCount], ys[i + j * NodeCount], zs[i + j * NodeCount], r);
    if DoClipHead[j] then begin
      CalcWheel(j, ClipHead[j], X, Y, Z, AXY, AZ);
      glPushMatrix;
      glTranslatef(X, Y, Z);
      glRotatef(AXY, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      EnableClipPlane(-1, 0, 0, 0);
      glPopMatrix;
      DrawSphere(X, Y, Z, r * 2);
    end;
    DisableClipPlane;
    if DoClipTail[j] then begin
      CalcWheel(j, ClipTail[j], X, Y, Z, AXY, AZ);
      glPushMatrix;
      glTranslatef(X, Y, Z);
      glRotatef(AXY, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
      EnableClipPlane(1, 0, 0, 0);
      glPopMatrix;
      DrawSphere(X, Y, Z, r * 2);
    end;
    DisableClipPlane;
  end;
end; // DrawPath

procedure CalcDoor(dt, OpenTime, CloseTime: extended;
                   var dDoorX: extended;
                   var dDoorY: extended;
                   var dDoorZ: extended;
                   typ: integer);
var dToClose: extended;
begin
  dDoorX := 0;
  dDoorY := 0;
  dDoorZ := 0;
  if typ = 3 then
    exit;
  if (dt <= OpenTime) or (dt >= CloseTime) then
    exit;

  dToClose := CloseTime - dt;
  // открытие и закрытие симметричны. dt смещаем к моменту открытия/закрытия
//  if dt - OpenTime < CloseTime - dt then
    dt := dt - OpenTime;
    CloseTime := CloseTime - OpenTime;
//  else
//    dt := CloseTime - dt;

  // рассчитываем положение дверей 0 - закрыто, 1 - открыто
//  dt := dt * 2;
  if dt < 0 then
    dDoorX := 0
  else if dt < 0.5 then
    dDoorX := (1 - cos(2 * dt * pi / 2)) / 5
  else if dt < 1.5 then
    dDoorX := 1 + (2 * dt - 3) * 0.4
//  else if dt <= CloseTime - 1.5 then
//    dDoorX := 1

  else if dToClose < 0 then
    dDoorX := 0
  else if dToClose < 2 then
    dDoorX := (1 - cos(0.5 * dToClose * pi / 2)) / 5
  else if dToClose < 3 then
    dDoorX := 1 + (2 * (dToClose - 1.5) - 3) * 0.4
  else
    dDoorX := 1;

// отход от вагона
  if dt <= 0 then
    dDoorY := 0
  else if dt < 0.5 then
    dDoorY := sin(2 * dt * pi / 2) / 7
  else if dt < CloseTime - 2 then
    dDoorY := 1 / 7
  else if dt < CloseTime then
    dDoorY := sin(0.5 * dToClose * pi / 2) / 7
  else
    dDoorY := 0;

// движение по вертикали
//  dDoorZ := 0;
end; // CalcDoor

function CalcLed(dt, CloseTime: extended;): Boolean;
begin
  Result := False;
  if (dt < CloseTime - 5) or (dt > CloseTime) then
    exit;

  Result := Round(dt * 2) mod 2 = 1;
end; // CalcLed

function CalcTrain(dt: extended; typ: integer): extended;
// typ: 0 - стоит на платформе, 1 - обычный, 2 - обратный, т.е. тупик, 3 - проходной
var L: extended;
begin
  // рассчитываем торможение, стоянку и разгон. Стоянка - с 0-й до 6-й сек.
  if typ = 0 then
    L := 0
  else if (typ = 1) or (typ = 2) then begin
    if dt < -3 then
      L := - dt ^ 2 - 2 * dt - 5
    else if dt < -2 then
      L := 4 * dt + 4
    else if dt < 0 then
      L := - dt ^ 2
    else if dt < StopTime then
      L := 0               // стоянка
    else
      L := (dt - StopTime) ^ 1.5; // разгон
    if (typ = 2) and (L < 0) then
      L := -L;
  end else if typ = 3 then
    L := 4 * dt + 4;
  Result := L;
end; // CalcTrain

procedure DrawTrainClock(TrainNumber: integer);
var Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1: extended;
begin
  // часы
  if ShowClock[TrainNumber] then begin
//ShowMessage('1');
    CalcWheel(TrainNumber, Clocks[TrainNumber], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z + 130);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    DrawClock(t - tDeparture[TrainNumber]);
    glPopMatrix;
  end;
end; // DrawTrainClock

procedure DrawSingleTrain(TrainNumber: integer; LeftOpened: Boolean; typ: integer);
var dt, dDoorX, dDoorY, dDoorZ, LHead: extended;
    i, SegmentHead, SegmentTail: integer;
    WheelX, WheelY, WheelZ: extended;
    Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1: extended;
    Wheel2X, Wheel2Y, Wheel2Z, AngleXY2, AngleZ2: extended;
    Wheel3X, Wheel3Y, Wheel3Z, AngleXY3, AngleZ3: extended;
    StraightTrain: Boolean;
    VagonName: string;
begin
  dt := t - tArrive[TrainNumber];

  if typ = 0 then
    dt := 3;

  if (dt < MinTime) or (dt > MaxTime) then
    exit;

  // запоминаем, когда поезд отправился, для часов
  if (dt > StopTime + 3) and (tDeparture[TrainNumber] < tArrive[TrainNumber]) then
    tDeparture[TrainNumber] := Round(t) + 0.5;

  LHead := CalcTrain(dt, typ);

  // обрезка
  if (LHead > 0) and DoClipHead[TrainNumber] then begin
    CalcWheel(TrainNumber, ClipHead[TrainNumber], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    EnableClipPlane(-1, 0, 0, 0);
    glPopMatrix;
  end else if (LHead < 0) and DoClipTail[TrainNumber] then begin
    CalcWheel(TrainNumber, ClipTail[TrainNumber], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    EnableClipPlane(1, 0, 0, 0);
    glPopMatrix;
  end;

  // при помощи коэффициентов приводим к масштабу
  LHead := -50 * LHead + Stops[TrainNumber];
  CalcDoor(dt, 0.5, StopTime - 0.5, dDoorX, dDoorY, dDoorZ, typ);
  dDoorX := dDoorX * 20;
  dDoorY := dDoorY * 20;
  dDoorZ := dDoorZ * 20;

  // определяем, рисовать ли поезд повагонно, или целиком.
  StraightTrain := NodeCount = 2;
  if not StraightTrain then begin
    SegmentHead := Path2Segment(TrainNumber, LHead - tels[0] - WheelBase / 2) - NodeCount * TrainNumber;
    SegmentTail := Path2Segment(TrainNumber, LHead - tels[TelsCount - 1] + WheelBase / 2) - NodeCount * TrainNumber;
    if SegmentTail > NodeCount - 2 then
      SegmentTail := NodeCount - 2;
    if SegmentHead > NodeCount - 2 then
      SegmentHead := NodeCount - 2;
    StraightTrain := SegmentHead = SegmentTail;
  end;

//StraightTrain := True;
  if not StraightTrain then
    for i := 0 to TelsCount - 1 do begin
      // тележки
      CalcWheel(TrainNumber, LHead - tels[i] + WheelBase / 2, Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
      CalcWheel(TrainNumber, LHead - tels[i] - WheelBase / 2, Wheel2X, Wheel2Y, Wheel2Z, AngleXY2, AngleZ2);
      AngleXY1 := Arctan2(Wheel2Y - Wheel1Y, Wheel2X - Wheel1X) * 57.3;
      AngleZ1 := -Arctan2(Wheel2Z - Wheel1Z, sqrt(sqr(Wheel2X - Wheel1X) + sqr(Wheel2Y - Wheel1Y))) * 57.3;
      Wheel1X := (Wheel1X + Wheel2X) / 2;
      Wheel1Y := (Wheel1Y + Wheel2Y) / 2;
      Wheel1Z := (Wheel1Z + Wheel2Z) / 2;


      if (i = 1) or (i = 4) then begin
        glPushMatrix;
        glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
        glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
        glRotatef(AngleZ1, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y
        glTranslatef(-Tels[1], 0, 0);
        Draw3dObject('Rusich.Tel2');
        glPopMatrix;
      end else begin
        glPushMatrix;
        glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
        glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
        glRotatef(AngleZ1, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y
        Draw3dObject('Rusich.Tel');
        glPopMatrix;
      end;
      
      if (i <> 0) and (i <> 3) then begin
        // вагон
        glPushMatrix;
        glTranslatef(WheelX, WheelY, WheelZ);
        glRotatef(Arctan2(WheelY - Wheel1Y, WheelX - Wheel1X) * 57.3, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
        glRotatef(-Arctan2(WheelZ - Wheel1Z, sqrt(sqr(WheelX - Wheel1X) + sqr(WheelY - Wheel1Y))) * 57.3, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y

        if i = 1 then 
          VagonName := 'Vagon1';

        if i = 2 then begin
          VagonName := 'Vagon2';
          glTranslatef(-Tels[1], 0, 0);
        end;

        if i = 4 then
          VagonName := 'Vagon3';

        if i = 5 then begin
          VagonName := 'Vagon4';
          glTranslatef(Tels[3]-Tels[4], 0, 0);
        end;

        // лампочки над дверьми
        if CalcLed(dt, StopTime - 0.5) then
          Draw3dObject('Rusich.' + VagonName + '.RedLed')
        else
          Draw3dObject('Rusich.' + VagonName + '.WhiteLed');

        if (dDoorX = 0) and (dDoorY = 0) and (dDoorZ = 0) then
          Draw3dObject('Rusich.' + VagonName + '.Closed')
        else begin

          if LeftOpened then begin
            // поезд
            Draw3dObject('Rusich.' + VagonName + '.LeftOpened');
            // рисуем двери
            glTranslatef(dDoorX, dDoorY, dDoorZ); // Левая дверь
            Draw3dObject('Rusich.' + VagonName + '.dverLL');
            glTranslatef(-2 * dDoorX, 0, 0); // Правая дверь
            Draw3dObject('Rusich.' + VagonName + '.dverLR');
          end else begin
            // поезд
            Draw3dObject('Rusich.' + VagonName + '.RightOpened');
            // рисуем двери
            glTranslatef(dDoorX, -dDoorY, dDoorZ); // Левая дверь
            Draw3dObject('Rusich.' + VagonName + '.dverRR');
            glTranslatef(-2 * dDoorX, 0, 0); // Правая дверь
            Draw3dObject('Rusich.' + VagonName + '.dverRL');
          end;
        end;

        glPopMatrix;
      end;

      WheelX := Wheel1X;
      WheelY := Wheel1Y;
      WheelZ := Wheel1Z;
    end
  else begin
      // поезд
    CalcWheel(TrainNumber, LHead - Tels[0], Wheel1X, Wheel1Y, Wheel1Z, AngleXY1, AngleZ1);
//ShowMessage(IntToStr(TrainNumber) + ' ' + FloatToStr(Wheel1X));

    glPushMatrix;
    glTranslatef(Wheel1X, Wheel1Y, Wheel1Z);
    glRotatef(AngleXY1, 0, 0, 1); // Поворачиваем сцену на угол вокруг оси Z
    glRotatef(-AngleZ1, 0, 1, 0); // Поворачиваем сцену на угол вокруг оси Y

    // лампочки над дверьми
    if CalcLed(dt, StopTime - 0.5) then
      Draw3dObject('Rusich.RedLed')
    else
      Draw3dObject('Rusich.WhiteLed');

    if (dDoorX = 0) and (dDoorY = 0) and (dDoorZ = 0) then
      Draw3dObject('Rusich.Train.Closed')
    else begin
      if LeftOpened then begin
        // поезд
        Draw3dObject('Rusich.Train.LeftOpened'); // поезд
        // двери
        glTranslatef(dDoorX, dDoorY, dDoorZ); // Левая дверь
        Draw3dObject('Rusich.dverLL'); // Левая дверь
        glTranslatef(-2 * dDoorX, 0, 0);
        Draw3dObject('Rusich.dverLR'); // Правая дверь
      end else begin
        // поезд
        Draw3dObject('Rusich.Train.RightOpened');// поезд
        // двери
        glTranslatef(dDoorX, -dDoorY, dDoorZ);
        Draw3dObject('Rusich.dverRR'); // Левая дверь
        glTranslatef(-2 * dDoorX, 0, 0);
        Draw3dObject('Rusich.dverRL'); // Правая дверь
      end;
    end;

    glPopMatrix;
  end;

  // отключаем обрезку
//  if DoClip[TrainNumber] then
  DisableClipPlane;
end; // DrawTrain

procedure DrawTrain(TrainNumber: integer; LeftOpened: Boolean; typ: integer);
begin
  DrawTrainClock(TrainNumber);

  DrawSingleTrain(TrainNumber, LeftOpened, typ);
end;

procedure GetInitPositions;
var i: integer;
begin
  for i := 0 to TrainCount - 1 do begin
    tDeparture[i] := Round(t - Period * Random(1)) + 0.5;
    tArrive[i] := tDeparture[i] - StopTime - 3;
  end;
end; // GetInitPositions

procedure GetPositionAbovePeriod;
var i: integer;
begin
  for i := 0 to TrainCount - 1 do
    if t > tArrive[i] + StopTime / 2 + Period / 2 then
      tArrive[i] := Round(tArrive[i] + Period + (Random(1) * 2 - 1) * Period * 0.25)
    else if t < tArrive[i] - Period then
      tArrive[i] := tArrive[i] - 86400;
end; // GetPositionAbovePeriod

procedure DrawTrains(LeftOpened: Boolean; typ: integer);
var i: integer;
begin
  for i := 0 to TrainCount - 1 do
    DrawTrain(i, LeftOpened, typ);
end; // DrawTrains

procedure DrawEscalatorStairs(Hour: integer; t: extended);
begin
  if (Hour >= 5) or (Hour = 0) then begin
    // ступени эскалатора, скорость - 1 ступень в секунду. Frac(t * v)
    DrawStairs('SUp', 1 - Frac(t));
    DrawStairs('SDown', Frac(t));
  end else begin
    DrawStairs('SUp', 0);
    DrawStairs('SDown', 0);
  end;
end;
